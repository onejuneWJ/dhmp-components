plugins {
    id "base"
    id "org.jetbrains.kotlin.jvm" apply false
}

ext {
    moduleProjects = subprojects.findAll { it.name.startsWith("dhmp-") }
    javaProjects = subprojects.findAll { !it.name.startsWith("component-") }
}

description = "dhmp components"

configure(allprojects) { project ->
    group = "com.zznode.dhmp"
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }
}

configure(allprojects - project(":component-platform")) {
    configurations {
        dependencyManagement {
            canBeConsumed = false
            canBeResolved = false
            visible = false
        }
        matching { it.name.endsWith("Classpath") }.all { it.extendsFrom(dependencyManagement) }
    }
    dependencies {
        dependencyManagement(enforcedPlatform(project(":component-platform")))
    }
}

configure([rootProject] + javaProjects) { project ->
    apply plugin: 'org.springframework.build.conventions'

}
configure(moduleProjects) { project ->
    apply from: "${rootDir}/gradle/dhmp-module.gradle"
}
